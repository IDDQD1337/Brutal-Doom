ACTOR PistolZombie: RifleZombie
{
	DropItem "PistolDrop"
	Tag "$TAGBD_ZOMBIEMAN2"
	States
	{
	JumpRollRight:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 20, 0, 0)
		TNT1 A 0 ThrustThing(angle*256/360+64, 8, 0, 0)
		ZRO1 IJ 6
		ZRO1 JJJJJJJJJJJJJJJJJJJJJJJJJ 1 A_CheckFloor("AvoidRightShort")
	AvoidRightShort:
		TNT1 A 0 ThrustThing(angle*256/360+64, 12, 0, 0)
		ZRO1 JKLMNOPI 2 A_FaceTarget
		TNT1 A 0 A_CheckSight("SeeContinue")
		TNT1 A 0 A_SpawnItemEX("ZombieStep",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_Stop
		Goto Missile+6

	JumpRollLeft:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 20, 0, 0)
		POSR A 0 ThrustThing(angle*256/360+192, 8, 0, 0)
		ZRO1 IP 6
		ZRO1 PPPPPPPPPPPPPPPPPPPP 1 A_CheckFloor("AvoidLeftShort")
	AvoidLeftShort:
		TNT1 A 0 ThrustThing(angle*256/360+192, 12, 0, 0)
		ZRO1 PONMLKJI 2 A_FaceTarget
		TNT1 A 0 A_CheckSight("SeeContinue")
		TNT1 A 0 A_SpawnItemEX("ZombieStep",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_Stop
		Goto Missile+6

	JumpLeft:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 20, 0, 0)
		POSR A 0 ThrustThing(angle*256/360+192, 8, 0, 0)
	LeftJumpLoop:
		ZRO1 I 1 A_FaceTarget
		ZRO1 I 1 A_CheckFloor("missile")
		Goto LeftJumpLoop
	JumpRight:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThingZ(0, 20, 0, 0)
		TNT1 A 0 ThrustThing(angle*256/360+64, 8, 0, 0)
	RightJumpLoop:
		ZRO1 I 1 A_FaceTarget
		ZRO1 I 1 A_CheckFloor("missile")
		Goto RightJumpLoop

	RollLeft:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+192, 15, 0, 0)
		ZRO1 PONMLKJI 2 A_FaceTarget
		TNT1 A 0 A_SpawnItemEX("ZombieStep",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_CheckSight("SeeContinue")
		TNT1 A 0 A_Stop
		Goto Missile+6

	RollRight:
		TNT1 A 0 A_ChangeFlag("NODROPOFF",true)
		POSR E 2 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+64, 15, 0, 0)
		ZRO1 IJKLMNOP 2 A_FaceTarget
		TNT1 A 0 A_SpawnItemEX("ZombieStep",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_CheckSight("SeeContinue")
		TNT1 A 0 A_Stop
		Goto Missile+6

	Spawn:
		PSPO A 2
		TNT1 A 0 A_GiveInventory("TypeZombieMan", 1)
		TNT1 A 0 A_TakeInventory("ZombiemanAmmo", 20)
		TNT1 A 0 A_GiveInventory("SKZombieMan", 1)
		TNT1 A 0 A_GiveInventory("ZombieManAmmo", random(1,10))
		TNT1 A 0 A_JumpIfInventory("SlaughterCounter", 200, "Stand")
		SHID A 0 A_RadiusGive("SlaughterCounter", 700, RGF_MONSTERS, 1)
		PSPO A 1
		Goto Stand

	Stand:
		PSSS B 0
		TNT1 A 0 A_CheckSight("Stand2")
		PSPO A 10 A_Look
		Loop

	Stand2:
		PSPO A 10 A_Look
		TNT1 A 0 A_CheckSight("Stand2")
		Goto Stand

	SearchForPlayer:
	Idle:
		TNT1 A 0 A_GiveInventory("SKZombieMan", 1)
		PSPO A 10 A_Look
		PSPO A 10 A_Look
		PSPO A 10 A_Look
		PSPO A 10 A_Look
		PSPO A 10 A_Look
		PSPO A 10 A_Look
	StartSearching:
		PSPO A 2 A_Look
		TNT1 A 0 A_Wander
		PSPO A 2 A_Look
		TNT1 A 0 A_Wander
		PSPO B 2 A_Look
		TNT1 A 0 A_Wander
		PSPO B 2 A_Look
		TNT1 A 0 A_Wander
		PSPO C 2 A_Look
		TNT1 A 0 A_Wander
		PSPO C 2 A_Look
		TNT1 A 0 A_Wander
		PSPO D 2 A_Look
		TNT1 A 0 A_Wander
		PSPO D 2 A_Look
		TNT1 A 0 A_Wander
		TNT1 A 0 A_Jump(64, "Idle")
		Goto StartSearching

	FallBack:
		TNT1 A 0 A_CheckSight("Advance")
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 1)
		PSPO D 2 A_FaceTarget
		PSPO D 2 A_Recoil(4)
		TNT1 A 0 A_jump(64, "Missile")
		PSPO C 2 A_FaceTarget
		PSPO C 2 A_Recoil(4)
		PSPO B 2 A_FaceTarget
		PSPO B 2 A_Recoil(3)
		PSPO A 2 A_FaceTarget
		PSPO A 2 A_Recoil(3)
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		GOto Missile

	See:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		TNT1 A 0 A_CheckSight("SeeNeverSeen")
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		Goto SeeContinue

	SeeContinue:
		TNT1 A 0 A_JumpIfCloser(200, "FallBack")
		Advance:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		PSPO AAA 2 A_Chase
		PSPO BBB 2 A_Chase
		PSPO CCC 2 A_Chase
		PSPO DDD 2 A_Chase
		TNT1 A 0 A_JumpIfInTargetInventory("IsInvisible", 1, "Idle")
		Loop

	SeeNeverSeen:
	Advance:
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		PSPO AA 2 A_Chase
		PSPO BB 2 A_Chase
		PSPO CC 2 A_Chase
		PSPO DD 2 A_Chase
		TNT1 A 0 A_CheckSIght("SeeNeverSeen")
		Goto SeeContinue

	Pain.Taunt:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Enraged", 1, "Missile")
		PSPO B 0 A_Pain
		TNT1 A 0 A_ChangeFlag("FASTER", 1)
		TNT1 A 0 A_ChangeFlag("MISSILEMORE", 1)
		TNT1 A 0 A_ChangeFlag("MISSILEEVENMORE", 1)
		TNT1 A 0 A_PlaySound("grunt/sight")
		PSPO B 1 A_FaceTarget
		PSPO B 10
		TNT1 A 0 A_GiveInventory("Enraged", 1)
		TNT1 A 0 A_PlaySound("grunt/sight", 1)
		Goto Missile

	Pain.Head:
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		POSH G 5 A_Pain
		TNT1 A 0 A_FaceTarget
		POSH H 20
		POSH H 20
		Goto SeeContinue

	Missile:
		TNT1 A 0 A_Stop
		TNT1 A 0 A_ChangeFlag("GHOST", 0)
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		TNT1 A 0 A_Jump( CallACS("AgileZombies", 0, 0, 0) * 64,  "RollLeft","JumpRollLeft",	"RollRight", "JumpRollRight" )
		PSPO E 14 A_FaceTarget
		TNT1 A 0 A_ChangeFlag("NODROPOFF",false)
		TNT1 A 0 A_JumpIfInventory("SlaughterCounter", 20, "FireGunSlaughter")
	FireGun:
		TNT1 A 0 A_GiveInventory("ZombieManAmmo", 1)
		TNT1 A 0 A_PlaySound("Pistol/Fire")
		TNT1 A 0 A_CustomMissile("DistantFireSoundRifle", 32, 0, 0, 1, 0)
		PSPO F 2 BRIGHT A_CustomMissile("ZombieManTracer", 48, 0, random(-2, 2), 1, random(-2, 2))
		TNT1 A 0 A_SpawnItem ("PistolCaseSpawn", 0, 30,0)
		PSPO E 12 A_FaceTarget
		TNT1 A 0 A_Jump(96, "FireGun")
		TNT1 A 0 A_JumpIfInventory("ZombieManAmmo", 15, "Reload")
		TNT1 A 0 A_JumpIfInTargetInventory("IsInvisible", 1, "SearchForPlayer")
		Goto SeeContinue

	FireGunSlaughter:
		TNT1 A 0 A_GiveInventory("ZombieManAmmo", 1)
		TNT1 A 0 A_PlaySound("Pistol/Fire")
		TNT1 A 0 A_CustomMissile("DistantFireSoundRifle", 32, 0, 0, 1, 0)
		ZSP1 F 2 BRIGHT A_CustomMissile("ZombieManTracer_SlaughterFest", 48, 0, random(-2, 2), 1, random(-2, 2))
		PSPO E 12 A_FaceTarget
		TNT1 A 0 A_Jump(96, "FireGunSlaughter")
		TNT1 A 0 A_JumpIfInventory("ZombieManAmmo", 15, "Reload")
		TNT1 A 0 A_JumpIfInTargetInventory("IsInvisible", 1, "SearchForPlayer")
		Goto SeeContinue

	Reload:
		TNT1 A 0 A_TakeInventory("ZombieManAmmo", 20)
		PSPR A 1
		PSPR A 1 A_CustomMissile("EmptyClipSpawn", 38, 0, random(-11, 11), 0)
		TNT1 A 0 A_PlaySound("Reload")
		PSPR ABACDED 3
		Goto SeeContinue

	Pain.Avoid:
		TNT1 A 0
		TNT1 A 0 A_Jump(256, "AvoidLeft", "AvoidRight")
		Goto AvoidLeft

	AvoidLeft:
		PSPO A 1 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+192, 15, 0, 0)
		ZRO2 ABCDEFGH 3 A_FaceTarget
		Goto Missile

	AvoidRight:
		PSPO A 1 A_FaceTarget
		TNT1 A 0 ThrustThing(angle*256/360+64, 15, 0, 0)
		ZRO1 ABCDEFGH 3 A_FaceTarget
		Goto Missile

	//If the monster is hit by the "Killme" damage (which is "exploded" by the captured marine), automatically start firing at the target.
	Pain.KillMe:
		TNT1 A 0
		TNT1 A 0 HEalThing(1)
		Goto Missile
	//Emergency manuever in case the monster gets killed by an "alerter" actor
	Death.KillMe:
	Death.Taunt:
	Death.Avoid:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 A_SpawnItem("PistolZombie")
		TNT1 A 0
		Stop

	Death.Minor:
		Goto DeathMirror

	Pain.Cut:
	Pain.Saw:
		TNT1 A 0
		TNT1 A 0 A_Stop
	Pain:
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, 8)
		PSPO G 2
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		PSPO G 2 A_Pain
		TNT1 A 0 A_Jump(196, "Missile")
		Goto SeeContinue
		TNT1 AAAA 0
		TNT1 A 0 A_Pain
		ID11 F 6
		Goto GetUp

	Pain.LowKick:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("IsDown", 1, "Death.LowKick")
		Goto Pain

	Death.LowKick:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("GoFatality", 1)
		TNT1 A 0 A_GiveToTarget("IsCurbstompingZombieman", 1)
		Stop

	Pain.Melee:
	Pain.Fatality:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory ("SKZombieman", 1, "GotoStealthKill")
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, 8)
		PSPO G 4
		PSPO G 4 A_Pain
		Goto SeeContinue
		TNT1 AAAA 0
		TNT1 A 0 A_Pain
		ID11 FRRRQ 10
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		TNT1 A 0 A_TakeInventory("IsDown", 1)
		Goto SeeContinue

	GotoStealthKill:
		TNT1 A 1
		//TNT1 A 0 A_JumpIfInTargetInventory("PowerStrength", 1, "BecomeMeatShield")
		TNT1 A 0 A_Explode(100, 1, 1)
		TNT1 A 1
		Goto SeeContinue

	BecomeMeatShield:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveToTarget("HasZManShield", 1)
		TNT1 A 0 A_GiveToTarget("GotMeatShield", 1)
		Stop

	Pain.ExtremePunches:
	Pain.ExplosiveImpact:
	Pain.Explosive:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag("NOBLOCKMONST", 1)
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_Pain
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, "Pain")
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		TNT1 A 0 A_GiveInventory("IsDown", 1)
		TNT1 A 0 ThrustThingZ(0,40,0,1)
		POS4 UVWXY 5
		Goto FallingAfterImpact

	Pain.Slide:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag("NOBLOCKMONST", 1)
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
		TNT1 A 0 A_ChangeFlag("GHOST", 1)
		TNT1 A 0 A_ChangeFlag("THRUGHOST", 1)
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 1)
		POS4 U 0 A_Pain
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD", 1)
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, "Pain")
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		POS4 U 1 A_facetarget
		TNT1 A 0 ThrustThingZ(0,50,0,1)
		TNT1 A 0 A_FaceTarget
		POS4 U 3
		TNT1 A 0 A_Recoil(-8)
		TNT1 A 0 A_GiveInventory("IsDown", 1)
		TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
		TNT1 A 0 A_ChangeFlag("GHOST", 0)
		TNT1 A 0 A_ChangeFlag("THRUGHOST", 0)
		POS4 UVWXY 3
		Goto FallingAfterImpact

	Pain.Kick:
	Pain.HeavyImpact:
		TNT1 A 0
		TNT1 A 0 A_ChangeFlag("NOBLOCKMONST", 1)
		TNT1 A 0 A_ChangeFLag("NODROPOFF", 0)
		TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
		TNT1 A 0 A_ChangeFlag("GHOST", 1)
		TNT1 A 0 A_ChangeFlag("THRUGHOST", 1)
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 1)
		POS4 U 0 A_Pain
		TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD", 1)
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, "Pain")
		TNT1 A 0 A_TakeInventory("SKZombieman", 1)
		POS4 U 1 A_facetarget
		TNT1 A 0 ThrustThingZ(0,30,0,1)
		TNT1 A 0 A_FaceTarget
		POS4 U 3 ThrustThing(angle*256/360+128, 10, 0, 0)
		TNT1 A 0 A_GiveInventory("IsDown", 1)
		POS4 UVWXY 3
		TNT1 A 0 A_ChangeFlag("GHOST", 0)
		TNT1 A 0 A_ChangeFlag("THRUGHOST", 0)
		Goto FallingAfterImpact

	FallingAfterImpact:
		ID11 F 1 A_GiveInventory ("FallingHeight", 1)
		ID11 F 1 A_CheckFloor ("GetUp")
		NULL A 0 A_JumpIf (momz == 0, "GetUp")
		Loop

	GetUp:
		TNT1 A 0
		MARN A 0 A_SpawnItem("bdbodythud")
		TNT1 A 0 A_JumpIfInventory ("FallingHeight", 5, "FallingDie")
		TNT1 A 0 A_TakeInventory("FallingHeight", 100)
		TNT1 A 0 A_Stop
		ID11 RRRRRRRRRRRRRR 5 A_JumpIf(momz < 0, "FallingAfterImpact")
		TNT1 A 0 A_TakeInventory("IsDown", 1)
		ID11 Q 10
		TNT1 A 0 A_ChangeFlag("FORCEXYBILLBOARD", 0)
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		Goto SeeContinue

	Active:
		TNT1 A 0
		TNT1 A 0 A_GiveToTarget("ExecuteDownedZombieman", 1)
		TNT1 A 0 A_GiveToTarget("GoSpecial", 1)
		IZD4 M 0
		ID11 RRRRRR 2 A_JumpIfInTargetInventory("IsExecutingOnAir", 1, "FinishActive")
		TNT1 A 0 A_JumpIfInTargetInventory("ExecuteBashWall", 1, "FinishActive")
		TNT1 M 1
		TNT1 A 0 A_JumpIfInTargetInventory("ExecuteStompWall", 1, "GetUp")
	FinishActive:
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_ChangeFlag("SHOOTABLE", 0)
		TNT1 A 0 A_ChangeFlag("THRUACTORS", 1)
		TNT1 A 0 A_ChangeFlag("COUNTKILL", 0)
		TNT1 A 0 A_ChangeFlag("USESPECIAL", 0)
		Stop

	Death.Minigun:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory ("IsDown", 1, "Death.Down")
		//TNT1 A 0 A_Jump(64, "Death.Rare")Don't inherit this state from riflezombie
		TNT1 A 0 A_Jump(16, "Death.Saw")
		TNT1 A 0 A_Jump(9, "Death.LegMassacre")
		TNT1 A 0 A_Jump(64, "Death")
		//Goto Death.Minigun
	Death.cut:
	Death.HKFT:
		TNT1 A 0
		TNT1 A 0 A_Scream
		TNT1 O 0 A_NoBlocking
		POSR Q 8
		TNT1 A 0 A_CustomMissile ("MuchBlood2", 35, 0, random (0, 360), 2, random (0, 160))
		TNT1 A 0 A_CustomMissile ("RipZombieMan", 0, 0, random (0, 360), 2, random (0, 160))
		TNT1 AAAA 0 A_CustomMissile ("Brutal_LiquidBlood", 30, 0, random (0, 360), 2, random (0, 40))
		POSX BCDE 9 A_CustomMissile ("Brutal_LiquidBlood", 30, 0, random (0, 360), 2, random (0, 40))//A-frames show the rifle
		TNT1 A 0 A_SpawnItem ("DeadZombieman_Half")
		TNT1 A 0 A_SpawnItem ("GrowingBloodPool")
		POSX EEEEEEEEEEEEEEEEEEEEEE 1 A_CustomMissile ("Brutal_LiquidBlood", 12, 0, random (0, 360), 2, random (0, 90))
		POSX E 0
		TNT1 A 0 A_SpawnItem("DeadZombiemanJustLegsAreLeft")
		TNT1 A 0 A_GiveToTarget("SoulAmmo", 10)
		TNT1 A -1
		Stop
	}
}