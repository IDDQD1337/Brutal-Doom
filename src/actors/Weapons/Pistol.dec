ACTOR BrutalPistol : BrutalWeapon
{
	Weapon.AmmoType1 "Clip1"
	Weapon.AmmoType2 "BDPistolAmmo"
	weapon.selectionorder 1900
	+WEAPON.WIMPY_WEAPON
	Inventory.PickupSound "Pistol/Draw"
	Inventory.PickupMessage "$GOTPISTOL"
	Obituary "$OBBD_PISTOL"
	Tag "$TAGBD_PISTOL"
	States
	{
	KickFlash: 
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristKickFlash")
		6IST ABCD 1
		6IST E 10
		6IST DCBA 1
		stop
	SlideFlash: 
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlash")
		6IST ABCD 1
		6IST E 36
		stop
	SlideFlashEnd: 
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlashEnd")
		6IST E 5
		6IST DCBA 1
		stop
	SlideFlashEndEarly: 
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristSlideFlashEndEarly")
		6IST E 2
		6IST DCBA 1
		stop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		PIST F 0 A_Takeinventory("DualPistols",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 1) )
		PIST E 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("SwitchDuals", 1, "SelectDual")
		TNT1 A 1
		PIST E 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		TNT1 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"selectAnimation")
		Goto FirstSelectAnimation
	
	SelectDual:
		TNT1 A 0 A_Takeinventory("SwitchDuals",1)
		PI2G LKJ 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 F 0 A_PlaySound("Pistol/Draw",5)
		PI2G IHG 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("SwitchDuals",1)
		Goto Ready
		
	FirstSelectAnimation:
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("NeverSelectedPistol", 1,2)
		Goto SelectAnimation
		tnt1 AA 0
		TNT1 A 0 A_TakeInventory("NeverSelectedPistol",1)
		PIST E 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		TNT1 A 3
		PISS ABC 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR ABCDEFGG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR HIJKKLLL 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR MNO 1 A_WeaponReady(WRF_ALLOWZOOM)
		PIST E 0 A_PlaySound("Pistol/Reload2", 5)
		PISR PQRSTTT 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI QRSTUU 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI EE 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Pistol/slide", 5)
		PISI FG 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI H 4 A_WeaponReady(WRF_ALLOWZOOM)
		PISI H 0 A_PlaySound("pistol/slide2", 5)
		PISI IJKLLL 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI MMNOP 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR BA 1 A_WeaponReady(WRF_ALLOWZOOM)
		PIST E 0 A_Takeinventory("Reloading",1)
		Goto Ready

	SelectAnimation:
		PIST E 0 A_PlaySound("Pistol/Draw", 5)
		2ISS A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,4)
		PISS A 0
		"####" E 0 A_JumpIfInventory("BDPistolAmmo", 1, 2)
		Goto SelectAnimationEmpty
		"####" AA 0
		"####" ABC 1 A_WeaponReady(WRF_NOFIRE)
		"####" E 0 A_Takeinventory("StartDualWield",1)
		Goto Ready

	SelectAnimationEmpty:
		PISS GHI 1 A_WeaponReady(WRF_NOFIRE)
		PIST E 0 A_Takeinventory("StartDualWield",1)
	Ready3:
	Ready:
		PIST E 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		PIST E 0 A_JumpIfInventory("Kicking",1,"DoKick")
		PIST E 0 A_JumpIfInventory("Taunting",1,"Taunt")
		PIST E 0 A_JumpIfInventory("Salute1", 1, "Salute")
		PIST E 0 A_JumpIfInventory("Salute2", 1, "Salute")
		PIST E 0 A_JumpIfInventory("Reloading",1,"Reload")
		PIST E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		PIST E 0 A_Takeinventory("PistolFireAnimation1",1)
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,3)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		PIST I 0
		"####" "#" 1 A_WeaponReady//(WRF_ALLOWRELOAD)
		"####" "#" 0 A_JumpIfInventory("Unloading",1,"Unload")
		"####" "#" 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		"####" "#" 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		Loop

	StartSprint:
		TNT1 A 0 A_TakeInventory("NeverSelectedPistol",1)
		6IST ABC 1 A_WeaponReady(WRF_NOFIRE)
		PIST E 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		PISP A 0 offset(-9,32)
		//PIST E 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		PIST E 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		PLAY A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		PIST E 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		PIST E 0 offset(-9,32) A_GiveInventory("UsedStamina", 2)
		PIST E 0 offset(-9,32)
		6IST E 1 offset(-9,34) A_SetPitch(pitch -0.5)
		6IST E 1 offset(-6,36) A_SetPitch(pitch -0.5)
		6IST E 1 offset(-3,38) A_SetPitch(pitch -0.5)
		6IST E 1 offset(0,38) A_SetPitch(pitch -0.5)
		6IST E 1 offset(3,36) A_SetPitch(pitch -0.5)
		6IST E 1 offset(6,34) A_SetPitch(pitch -0.5)
		6IST E 1 offset(9,32) A_SetPitch(pitch -0.5)
		PIST E 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		PIST E 0 A_JumpIfInventory("Kicking",1,"DoKick")
		PIST E 0 A_JumpIfInventory("Taunting",1,"Taunt")
		PIST E 0 A_JumpIfInventory("Reloading",1,"Reload")
		PIST E 0 offset(-9,32) A_GiveInventory("UsedStamina", 3)
		PIST E 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		6IST E 1 offset(9,34) A_SetPitch(pitch +0.5)
		6IST E 1 offset(6,36) A_SetPitch(pitch +0.5)
		6IST E 1 offset(3,38) A_SetPitch(pitch +0.5)
		6IST E 1 offset(0,38) A_SetPitch(pitch +0.5)
		6IST E 1 offset(-3,36) A_SetPitch(pitch +0.5)
		6IST E 1 offset(-6,34) A_SetPitch(pitch +0.5)
		6IST E 1 offset(-9,32) A_SetPitch(pitch +0.5)
		PIST E 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		PIST E 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		6IST CBA 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		PIST E 0 A_PlaySound("Tired", 2)
		PIST E 0 A_TakeInventory("UsedStamina", 2)
		PIST E 5 A_WeaponReady
		PIST E 0 A_TakeInventory("UsedStamina", 2)
		PIST E 5 A_WeaponReady
		PIST E 0 A_TakeInventory("UsedStamina", 2)
		PIST E 5 A_WeaponReady
		PIST E 0 A_TakeInventory("UsedStamina", 2)
		PIST E 5 A_WeaponReady
		PIST E 0 A_TakeInventory("UsedStamina", 2)
		PIST E 5 A_WeaponReady
		Goto Ready

	StopSprint:
		PIST E 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		6IST CBA 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	Deselect:
		PIST E 0 A_TakeInventory("TossGrenade", 1)
		TNT1 A 0 A_TakeInventory("HoldInspect",1)
		TNT1 A 0 A_TakeInventory("IsInspecting",1)
		TNT1 A 0 A_JumpIfInventory("SwitchDuals", 1, "DeselectInstant")
		2ISS A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"DeselectContinue")
		PISS E 0 A_JumpIfInventory("BDPistolAmmo", 1, 1)
		Goto DeselectEmpty
		PISS E 0
	DeselectContinue:
		"####" DEF 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	DeselectEmpty:
		PISS JKL 1
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Fire:
		2IST A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"NoReload")
		PIST E 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		"####" E 0 A_JumpIfInventory("BDPistolAmmo",1,1)
		Goto NoAmmo
		"####" E 0 A_Takeinventory("BDPistolAmmo",1)
		"####" E 0 A_Takeinventory("BDDualPistolAmmo",1)
		"####" E 0 A_JumpIfInventory("BDPistolAmmo",6,2)
		"####" E 0 A_PlaySound("weapons/lowammo",CHAN_AUTO)
		"####" EEE 0
		Goto ResumeFireFrame

	NoReload:
		"####" E 0 A_JumpIfInventory("Clip1",1,1)
		Goto NoAmmo
		"####" E 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		"####" A 0 A_JumpIfInventory("Clip1",6,2)
		"####" E 0 A_PlaySound("weapons/lowammo",CHAN_AUTO)
		"####" EEE 0
	ResumeFireFrame:
		"####" A 0
		"####" E 0 A_AlertMonsters
		"####" E 0 A_PlaySound("FARSHT", 6)
		"####" E 0 A_PlaySound("Pistol/Fire", 1)
		"####" E 0 A_JumpIFInventory("PistolBurstFire", 1, "Fire2")		
		"####" E 0 A_ZoomFactor(0.98)
		"####" A 1 BRIGHT A_FireBullets (1, 1, -1, 14, "WeakPuff", FBF_NORANDOM)
		"####" E 0 A_ZoomFactor(1.0)
		"####" A 0 A_FireCustomMissile("DecorativeTracer", random(-1,1), 0, 0, 0)
		"####" B 1
		"####" E 0 A_FireCustomMissile("PistolCaseSpawn",5,0,0,-9)
		"####" C 1
		"####" E 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		Goto NoAmmo2
		"####" EF 0
		"####" E 0 A_JumpIfInventory("IsTacticalClass", 1, "FireEndTactical")
		"####" D 1
		"####" E 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" E 1 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		"####" E 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" E 3 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		"####" E 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" E 3 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		"####" E 0 A_REfire
		Goto Ready

	Fire2:
		"####" A 0
		"####" E 0 A_GiveInventory("PistolBurstCounter",1)
		"####" E 0 A_ZoomFactor(0.98)
		"####" A 1 BRIGHT A_FireBullets (1, 1, -1, 14, "WeakPuff", FBF_NORANDOM)
		"####" E 0 A_ZoomFactor(1.0)
		"####" A 0 A_FireCustomMissile("DecorativeTracer", random(-1,1), 0, 0, 0)
		"####" B 1
		"####" E 0 A_FireCustomMissile("PistolCaseSpawn",5,0,0,-9)
		"####" E 0 A_JumpIfInventory("PistolBurstCounter",3,2)
		Goto Fire
		"####" AAA 0
		"####" EF 0
		"####" C 1 A_TakeInventory("PistolBurstCounter",0)
		"####" DE 1	
		"####" E 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" E 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" E 2 A_WeaponReady(WRF_NOBOB | WRF_ALLOWRELOAD)
		Goto Ready

	FireEndTactical:
		PIST E 0
		PIST E 0 A_JumpIfInventory("IsRunning", 1, "FireEndTactical2")
		PIST E 1 A_WeaponReady(1)
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		PIST E 7 A_WeaponReady(1)
		Goto Ready

	FireEndTactical2:
		PLAY A 0 ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		PIST E 0 A_GiveInventory("UsedStamina", 4)
		PIST E 1 A_WeaponReady(1)
		PIST E 5 A_WeaponReady(1)
		Goto Sprinting

	NoAmmo:
		TNT1 A 0 A_TakeInventory("PistolBurstCounter",0)
		RIFG A 0 A_ZoomFactor(1.0)
		RIFG A 0 A_Takeinventory("Zoomed",1)
		RIFG A 0 A_Takeinventory("ADSmode",1)
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		PIST I 0
		"####" "#" 1 offset(0,33)
		"####" "#" 1 offset(0,32)
	NoAmmo2:
		2IST E 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		PIST I 0
		"####" "#" 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" "#" 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" "#" 0 A_JumpIfInventory("Salute1", 1, "Salute")
		"####" "#" 0 A_JumpIfInventory("Salute2", 1, "Salute")
		"####" "#" 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		"####" "#" 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		"####" "#" 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" "#" 3 A_WeaponReady(WRF_ALLOWZOOM | WRF_NOFIRE)
		"####" "#" 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" "#" 0 A_JumpIfInventory("BDPistolAmmo", 1, "Ready3")
		"####" "#" 0 A_JumpIfInventory("NoAutoReload", 1, "NoAmmo2")
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		"####" "#" 0 A_JumpIfInventory("Clip1",1,"Reload")
		"####" "#" 0 A_JumpIfInventory("FiredPrimary",1,"NoAmmo2")
		Goto Ready3

	Inspect:
		PIST E 0 A_JumpIfInventory("DoInspect",1,"ContinueInspect")
		PIST E 0 A_JumpIfInventory("FiredSecondary",1,"ContinueInspect")
		PIST E 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		PIST E 0 A_JumpIfInventory("Kicking",1,"DoKick")
		PIST E 0 A_JumpIfInventory("Taunting",1,"Taunt")
		PIST E 0 A_JumpIfInventory("Salute1", 1, "Salute")
		PIST E 0 A_JumpIfInventory("Salute2", 1, "Salute")
		PIST E 0 A_JumpIfInventory("Reloading",1,"Reload")
		PIST E 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		PIST E 0 A_JumpIfInventory("Unloading",1,"Unload")
		PIST E 0 A_Takeinventory("PistolFireAnimation1",1)
		PIST E 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		PIST E 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		PIST E 6 A_WeaponReady(WRF_ALLOWZOOM)
		PIST E 0 A_GiveInventory("DoInspect",1)
		PIST E 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		PIST E 0 A_TakeInventory("DoInspect",1)
		Goto Ready3

	ContinueInspect:
		TNT1 AA 0
		PIST E 0 A_TakeInventory("DoInspect",1)
		PISR AB 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI ABCDEEEEE 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("Pistol/slide", 5)
		PISI FGHH 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_GiveInventory("HoldInspect",1)
	Inspect2:
		PISI H 2 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("HoldInspect",1)
		PISI H 0 A_PlaySound("pistol/slide2", 5)
		PISI IJKLLL 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISI MMNOP 1 A_WeaponReady(WRF_ALLOWZOOM)
		PISR BA 1 A_WeaponReady(WRF_ALLOWZOOM)
		PIST E 0 A_Takeinventory("Reloading",1)
		Goto Ready

	Reload:
		"####" "#" 0 A_Takeinventory("Reloading",1)
		"####" "#" 0 A_Takeinventory("ADSmode",1)
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		"####" "#" 0 A_JumpIfInventory("HoldInspect",1,"Inspect2")
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",0,"Inspect")
		PIST E 0 A_JumpIfInventory("Clip1",1,3)
		Goto Ready
		TNT1 AAA 0
		PIST E 0 A_Takeinventory("Zoomed",1)
		PIST E 0 A_ZoomFactor(1.0)
		PIST E 0 A_GiveInventory ("Pumping", 1)
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_JumpIfInventory("HasUnloadedPistol", 1, 3)
		RIFR C 0 A_FireCustomMissile("EmptyClip2Spawn",-5,0,8,-4)
		PIST E 0 A_PlaySound("Pistol/Reload1", 5)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,"Reload2")//15+1 effect
		TNT1 AA 0
		2ISR ABCDEFGG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2ISR HIJKKLLL 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2ISR MNO 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2ISR F 0 A_PlaySound("Pistol/Reload2", 5)
	InsertBullets:
		TNT1 AAAA 0		
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",15,15)
		PIST E 0 A_JumpIfInventory("Clip1",1,3)
		Goto FinishReload
		TNT1 AAAAAA 0
		PIST E 0 A_Giveinventory("BDPistolAmmo",1)
		PIST E 0 A_Giveinventory("BDDualPistolAmmo",1)
		PIST E 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets
		TNT1 AAAAAAAAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto FinishReload
		TNT1 AAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto FinishReload

	FinishReload:
		2ISR PQRSS 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2ISR T 1 A_PlaySound("pistol/slide2", 5)
		2ISR UV 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR TTUVW 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR DCBA 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST E 0 A_Takeinventory("HasUnloadedPistol",1)
		PIST E 0 A_Takeinventory("HasUnloadedDualPistols",1)
		Goto Ready

	Reload2: //empty
		TNT1 AA 0
		PISR ABCDEFGG 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR HIJKKLLL 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR MNO 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST E 0 A_PlaySound("Pistol/Reload2", 5)
	InsertBullets2:
		TNT1 AAAA 0
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",0,15)
		PIST E 0 A_JumpIfInventory("Clip1",1,3)
		Goto FinishReload2
		TNT1 AAAAAA 0
		PIST E 0 A_Giveinventory("BDPistolAmmo",1)
		PIST E 0 A_Giveinventory("BDDualPistolAmmo",1)
		PIST E 0 A_Takeinventory("Clip1",1,TIF_NOTAKEINFINITE)
		Goto InsertBullets2
		TNT1 AAAAAAAAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto FinishReload2
		TNT1 AAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto FinishReload2

	FinishReload2:
		PISR PQRSTT 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR UVW 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PISR DCBA 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		PIST E 0 A_Takeinventory("HasUnloadedPistol",1)
		PIST E 0 A_Takeinventory("HasUnloadedDualPistols",1)
		Goto Ready

	Spawn:
		PIST A 0
		P3ST K -1
		Stop

	AltFire:
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		TNT1 A 0 A_JumpIFInventory("PistolBurstFire", 1, "StopAlt")
		TNT1 A 0 A_GiveInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("Burst fire",1)
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		PIST I 0
		PIST "#" 1 offset(0,34)
		PIST "#" 1 offset(0,33)
		Goto Ready3

	StopAlt:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("PistolBurstFire", 1)
		TNT1 A 0 A_Print("Single shot",1)
		TNT1 A 0 A_PlaySound("Weapon/Empty", 5)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,2)
		PIST I 0
		PIST "#" 1 offset(0,34)
		PIST "#" 1 offset(0,33)
		Goto Ready3

	Steady:
		TNT1 A 1
		Goto Ready

	Unload:
		"####" E 1 A_WeaponReady
		"####" E 0 A_Takeinventory("Unloading",1)
		"####" E 0 A_Takeinventory("ADSmode",1)
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		PIST E 0 A_JumpIfInventory("NoReloading",1,"Ready")
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,3)
		Goto NoAmmo
		TNT1 AAA 0
		PIST E 0 A_Takeinventory("Zoomed",1)
		PIST E 0 A_ZoomFactor(1.0)
		PIST E 0 A_GiveInventory ("Pumping", 1)
		PIST E 0 A_Takeinventory("Reloading",1)
		PISR ABCDWVUTSS 1
		PIST E 0 A_PlaySound("Pistol/Reload1", 5)
		2ISR SONMKJIIHG 1
		2ISR FEDCBA 1
		PIST E 0 A_Giveinventory("HasUnloadedPistol",1)
		PIST E 0 A_Giveinventory("HasUnloadedDualPistols",1)
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,"RemoveBullets")
	RemoveBullets:
		TNT1 AAAA 0
		PIST E 0 A_JumpIfInventory("BDPistolAmmo",1,3)
		Goto Ready
		TNT1 AAAAAA 0
		PIST E 0 A_Takeinventory("BDPistolAmmo",1)
		PIST E 0 A_Takeinventory("BDDualPistolAmmo",1)
		PIST E 0 A_Giveinventory("Clip1",1)
		Goto RemoveBullets
		TNT1 AAAAAAAAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto Ready
		TNT1 AAAA 0
		PIST E 0 A_Takeinventory("Reloading",1)
		PIST E 0 A_Refire
		Goto Ready

	DualWield:
		"####" "#" 0 A_Takeinventory("StartDualWield",1)
		"####" "#" 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		PIST "#" 0 A_JumpIfInventory("HasDualPistol", 1, "GetDual")
		PIST "#" 0 A_Print("You must have two identical weapons to dual wield.")
		Goto Ready

	GetDual:
		PIST E 0
		PIST E 0 A_Takeinventory("Zoomed",1)
		PIST E 0 A_Takeinventory("ADSmode",1)
		PIST E 0 A_TakeInventory("TossGrenade", 1)
		PIST E 0 A_TakeInventory("Kicking",1)
		PIST E 0 A_TakeInventory("Taunting",1)
		PIST E 0 A_TakeInventory("Reloading",1)
		PIST E 0 A_TakeInventory("Unloading",1)
		PIST E 0 A_TakeInventory("Salute1", 1)
		PIST E 0 A_TakeInventory("Salute2", 1)
		PIST E 0 A_ZoomFactor(1.0)
		TNT1 A 0 A_GiveInventory("SwitchDuals",1)
		PIST F 0 A_GiveInventory("DualPistols",1)
		PIST E 0 A_SelectWEapon("DualPistols")
		Goto Ready
	}
}