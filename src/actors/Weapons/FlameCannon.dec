ACTOR FlameCannon : BrutalWeapon
{
	Weapon.SelectionOrder 3400
	Weapon.AmmoType "Gas"
	Inventory.PickupSound "Flamer/select"
	-WEAPON.NOALERT
	+WEAPON.EXPLOSIVE
	-WEAPON.NOAUTOFIRE
	Inventory.PickupMessage "$GOTFLAMECANNON"
	Tag "$TAGBD_FLAMECANNON"
	Obituary "$OBBD_FLAMECANNON"
	States
	{
	KickFlash: 
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,"PuristKickFlash")
		FLMG TUVW 1
		FLMG X 10
		FLMG WVUT 1
		stop
	SlideFlash: 
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,"PuristSlideFlash")
		FLMG TUVW 1
		FLMG X 35
		stop
	SlideFlashEnd: 
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,"PuristSlideFlashEnd")
		FLMG X 5
		FLMG WVUT 1
		stop
	SlideFlashEndEarly: 
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,"PuristSlideFlashEndEarly")
		FLMG X 2
		FLMG WVUT 1
		stop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 A 0 A_Giveinventory("FlameCannonSelected",1)
		TNT1 A 0 A_TakeInventory("DualFlameCannon",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 15) )
		TNT1 F 0 A_GunFlash
		TNT1 A 0 A_JumpIfInventory("SwitchDuals", 1, "SelectDual")
		TNT1 A 1
		TNT1 A 0 A_PlaySound("Flamer/select", 5)
		Goto SelectAnimation
	SelectDual:
		TNT1 A 0 A_Takeinventory("SwitchDuals",1)
		DUFL "[" 1 A_WeaponReady(WRF_NOFIRE)
		DUFL ZY 1 A_WeaponReady(WRF_NOFIRE)
		FLMG "\" 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_PlaySound("Flamer/select",5)
		FLMG "][" 1 A_WeaponReady(WRF_NOFIRE)
		FLMG ZY 1 A_WeaponReady(WRF_NOFIRE)
		Goto Ready
		
	SelectAnimation:
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,2)
		FLMG A 0
		"####" LMNO 1 A_WeaponReady(WRF_NOFIRE)
		"####" F 0 A_Takeinventory("StartDualWield",1)
	Ready:
	Ready3:
	Reload:
		2LMG A 0 A_JumpIfInventory("IsPlayingAsPurist",1,2)
		FLMG A 0
		"####" A 1 A_WeaponReady//(WRF_ALLOWZOOM)
		"####" A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		"####" A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		"####" A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		"####" F 0 A_JumpIfInventory("StartDualWield",1,"DualWield")
		Goto Ready3

	StartSprint:
		FLMG TUV 1 A_WeaponReady(WRF_NOFIRE)
		FLMG A 0 A_ZoomFactor(1.0)
		FLMG A 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		FLMG A 0 offset(-9,32)
		FLMG A 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		PLAY A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		FLMG A 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		FLMG A 0 offset(-9,32) A_GiveInventory("UsedStamina", 8)
		FLMG A 0 offset(-9,32)
		FLMG W 1 offset(-9,34) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(-6,36) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(-3,38) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(0,38) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(3,36) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(6,34) A_SetPitch(pitch -0.5)
		FLMG W 1 offset(9,32) A_SetPitch(pitch -0.5)
		FLMG A 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		FLMG A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		FLMG A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		FLMG A 0 A_JumpIfInventory("Reloading",1,"Reload")
		FLMG A 0 offset(-9,32) A_GiveInventory("UsedStamina", 8)
		FLMG A 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		FLMG W 1 offset(9,34) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(6,36) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(3,38) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(0,38) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(-3,36) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(-6,34) A_SetPitch(pitch +0.5)
		FLMG W 1 offset(-9,32) A_SetPitch(pitch +0.5)
		FLMG A 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		FLMG A 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		FLMG VUT 1
		FLMG A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		FLMG A 0 A_PlaySound("Tired", 2)
		FLMG A 0 A_TakeInventory("UsedStamina", 2)
		FLMG A 5 A_WeaponReady
		FLMG A 0 A_TakeInventory("UsedStamina", 2)
		FLMG A 5 A_WeaponReady
		FLMG A 0 A_TakeInventory("UsedStamina", 2)
		FLMG A 5 A_WeaponReady
		FLMG A 0 A_TakeInventory("UsedStamina", 2)
		FLMG A 5 A_WeaponReady
		FLMG A 0 A_TakeInventory("UsedStamina", 2)
		FLMG A 5 A_WeaponReady
		Goto Ready

	StopSprint:
		FLMG A 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		FLMG VUT 1
		FLMG A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	DryFire:
		"####" A 6 A_PlaySound("Weapon/Empty", 5)
		Goto Ready+6

	Deselect:
		"####" A 0 A_JumpIfInventory("SwitchDuals", 1, "DeselectInstant")
		"####" A 0 A_TakeInventory("TossGrenade", 1)
		"####" PQRS 1
		"####" AAAAAAAAAAAAAAAAAA 0 A_Lower
		"####" A 1 A_Lower
		Wait

	Spawn:
		MANA H 1
		Loop

	Fire:
		"####" A 0
		"####" A 0 A_JumpIf(waterlevel > 1, "DryFire")
		"####" A 0 A_JumpIfInventory("Gas", 30, "Fireball")
		"####" A 0 A_StopSound(CHAN_WEAPON)
		Goto DryFire

	AltFire:
		"####" A 0
		"####" A 0 A_JumpIf(waterlevel > 1, "DryFire")
		"####" A 0 A_JumpIfInventory("Gas", 44, "Flamethrower")
		"####" A 0 A_StopSound(CHAN_WEAPON)
		Goto DryFire

	Fireball:
		"####" A 0
		"####" A 0 A_AlertMonsters
		"####" B 1 BRIGHT A_FireCustomMissile("BigFireBallWithGravity", 0, 1, 0, -6)
		"####" A 0 A_SetPitch(-4.0 + pitch)
		"####" A 0 A_TakeInventory("Gas", 30,TIF_NOTAKEINFINITE)
		"####" AAAAAAAAAAA 0 BRIGHT A_FireCustomMissile("Shotgunparticles2", random(-12,12), 0, 0, -2, 0, random(-9,9))
		"####" A 0 A_FireCustomMissile("ShakeYourAssDouble", 0, 0, 0, 0)
		"####" CD 1 BRIGHT
		"####" EA 1 A_SetPitch(+2.0 + pitch)
		"####" A 5
		Goto Ready+2

	Flamethrower:
		"####" A 0
		"####" A 0 A_AlertMonsters
		"####" A 0 BRIGHT A_FireCustomMissile("FlamethrowerMissile", 0, 1, 0, -12, 0, 8)
		"####" AAA 0 BRIGHT A_FireCustomMissile("Shotgunparticles2", random(-12,12), 0, 0, -2, 0, random(-9,9))
		"####" A 0 A_PlaySound("FLAMER", CHAN_WEAPON, 1, 1)
		"####" A 0 A_TakeInventory("Gas", 11,TIF_NOTAKEINFINITE)
		"####" HI 1 BRIGHT A_SetAngle(random(1, -1) + angle)
		"####" A 0 BRIGHT A_SetPitch(random(1, -1) + pitch)
		"####" A 0 BRIGHT A_FireCustomMissile("FlamethrowerMissileNoLight", 0, 1, 0, -12, 0, 8)
		"####" AAA 0 BRIGHT A_FireCustomMissile("Shotgunparticles2", random(-12,12), 0, 0, -2, 0, random(-9,9))
		"####" A 0 A_PlaySound("FLAMER", CHAN_WEAPON, 1, 1)
		"####" A 0 A_TakeInventory("Gas", 11,TIF_NOTAKEINFINITE)
		"####" JK 1 BRIGHT
		"####" A 0 BRIGHT A_FireCustomMissile("FlamethrowerMissileNoLight", 0, 1, 0, -12, 0, 8)
		"####" A 0 BRIGHT A_FireCustomMissile("Shotgunparticles2", random(-12,12), 0, 0, -2, 0, random(-9,9))
		"####" A 0 A_PlaySound("FLAMER", CHAN_WEAPON, 1, 1)
		"####" A 0 A_TakeInventory("Gas", 11,TIF_NOTAKEINFINITE)
		"####" HI 1 BRIGHT
		"####" A 0 BRIGHT A_FireCustomMissile("FlamethrowerMissileNoLight", 0, 1, 0, -12, 0, 8)
		"####" A 0 BRIGHT A_FireCustomMissile("Shotgunparticles2", random(-12,12), 0, 0, -2, 0, random(-9,9))
		"####" A 0 A_PlaySound("FLAMER", CHAN_WEAPON, 1, 1)
		"####" A 0 A_TakeInventory("Gas", 11,TIF_NOTAKEINFINITE)
		"####" EA 1
		"####" A 0 A_PlaySound("FLAMSTOP", CHAN_WEAPON)
		"####" A 14
		Goto Ready+6

	DualWield:
		"####" "#" 0
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		FLMG "#" 0 A_Takeinventory("StartDualWield",1)
		FLMG "#" 0 A_JumpIfInventory("HasDualFlameCannon", 1, "GetDual")
		FLMG "#" 0 A_Print("You must have two identical weapons to dual wield.")
		Goto Ready

	GetDual:
		FLMG F 0
		FLMG F 0 A_Takeinventory("Zoomed",1)
		FLMG F 0 A_Takeinventory("ADSmode",1)
		FLMG F 0 A_TakeInventory("TossGrenade", 1)
		FLMG F 0 A_TakeInventory("Kicking",1)
		FLMG F 0 A_TakeInventory("Taunting",1)
		FLMG F 0 A_TakeInventory("Reloading",1)
		FLMG F 0 A_TakeInventory("Unloading",1)
		FLMG F 0 A_TakeInventory("Salute1", 1)
		FLMG F 0 A_TakeInventory("Salute2", 1)
		FLMG A 0 A_GiveInventory("SwitchDuals",1)
		FLMG A 0 A_GiveInventory("DualFlameCannon",1)
		FLMG F 0 A_ZoomFactor(1.0)
		FLMG F 0 A_SelectWEapon("DualFlameCannon")
		Goto Ready
	}
}

ACTOR BigFireBallWithGravity: BigFireBall
{
	Damage (random (100, 100))
	Gravity 0.2
	//+NOGRAVITY
	Speed 50
	Damagetype "Fire"
	States
	{
	Death:
		FLMG A 0
		FLMG A 0 A_Explode (60, 150)
		Goto ExplosionEffect
	}
}

ACTOR Flames
{
	Radius 6
	Height 8
	Speed 30
	Damage 1
	Projectile
	+RANDOMIZE
	+FORCEXYBILLBOARD
	+RIPPER
	+BLOODLESSIMPACT
	RenderStyle Add
	DamageType Flames
	Scale 2.0
	Alpha 1
	SeeSound "fatso/attack"
	DeathSound "fatso/shotx"
	Decal "BigScorch"
	States
	{
	Spawn:
		TNT1 C 6
		Goto See

	See:
		FLMG A 0 A_Explode (15, 40)
		FRFX C 2 BRIGHT A_SpawnItem("RedFlareMedium",0,0)
		FLMG A 0 A_Explode (15, 40)
		FRFX C 2 BRIGHT A_SpawnItem("RedFlareMedium",0,0)
		FLMG A 0 A_Explode (15, 40)
		EXPL AA 0 A_CustomMissile ("ExplosionSmoke", 32, 0, random (0, 360), 2, random (0, 360))
		FLMG A 0 A_Explode (15, 40)
		FRFX FGHIJKLMNOPG 2 BRIGHT A_SpawnItem("RedFlareMedium",0,0)
		Goto Death

	Death:
		EXPL AA 0 A_CustomMissile ("ExplosionSmoke", 32, 0, random (0, 360), 2, random (0, 360))
		FLMG A 0 A_SpawnItem("RedFlareMedium",0,0)
		Stop
	}
}