ACTOR GrenadeLauncher : BrutalWeapon
{
	Weapon.AmmoType1 "AmmoRocket"
	Weapon.AmmoType2 "GLAmmo"
	Weapon.SelectionOrder 2600
	+WEAPON.EXPLOSIVE
	-WEAPON.NOAUTOFIRE
	Inventory.PickupSound "ROCKPKUP"
	Inventory.PickupMessage "$GOTGRENADELAUNCHER"
	Tag "$TAGBD_GL"
	Obituary "$OBBD_GRENADELAUNCHER"
	States
	{
	Inspect:
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"Ready")
		SHTN A 0 A_JumpIfInventory("DoInspect",1,"ContinueInspect")
		SHTN A 0 A_JumpIfInventory("FiredSecondary",1,"ContinueInspect")
		GRLN A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		GRLN A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		GRLN A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		GRLN A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		GRLN A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		GRLN A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		GRLN A 0 A_JumpIfInventory("IsRunning", 1, "StartSprint")
		GRLN A 6 A_WeaponReady(WRF_ALLOWZOOM)
		GRLN A 0 A_JumpIfInventory("Unloading", 1, "Unload")
		GRLN A 0 A_JumpIfInventory("Reloading", 1, "Reload")
		GRLN A 0 A_GiveInventory("DoInspect",1)
		GRLN A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		GRLN A 0 A_TakeInventory("DoInspect",1)
		Goto Ready3

	ContinueInspect:
		TNT1 AA 0
		GRLN A 0 A_TakeInventory("DoInspect",1)
		TNT1 A 0
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"Ready")
		TNT1 A 0 A_TakeInventory("RELOADING",1)
		GRLN FGHII 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRLN J 1 A_PlaySound("GrenadeLauncher/Open", 5)
		GRLN K 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL3 AABCDD 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL3 D 10
		TNT1 A 0 A_GiveInventory("HoldTheGL", 1)
	Inspect2:
		GRL3 D 4 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		TNT1 A 0 A_TakeInventory("HoldTheGL", 1)
		GRL3 D 1 Offset(1,34) A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL3 D 1 Offset(10,45) A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL3 D 1 Offset(25,60) A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL3 D 1 Offset(35,70) A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 4 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRLN Z 1 Offset(1,33) A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		tnt1 a 0 A_PlaySound("GrenadeLauncher/Close", 5)
		GRLN "[" 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRLN "\]" 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL2 ABCC 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		GRL2 DDEFGHIJKL 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("RELOADING",1)
		Goto Ready

	KickFlash: 
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristKickFlash")
		GRL2 XYZ 1
		GRL2 "[" 1
		GRL2 "]" 10
		GRL2 "[" 1
		GRL2 ZYX 1
		stop
	SlideFlash: 
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristKickFlash")
		GRL2 XYZ 1
		GRL2 "[" 1
		GRL2 "]" 35
		stop
	SlideFlashEnd: 
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristKickFlash")
		GRL2 "]" 5
		GRL2 "[" 1
		GRL2 ZYX 1
		stop
	SlideFlashEndEarly: 
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristKickFlash")
		GRL2 "]" 2
		GRL2 "[" 1
		GRL2 ZYX 1
		stop

	Select:
		TNT1 A 0 A_Giveinventory("GoSpecial",1)
		TNT1 A 0 A_GiveInventory("TokenCleaner")
		TNT1 A 0 A_Giveinventory("GrenadeLauncherSelected",1)
		TNT1 A 0 A_SetCrosshair( CallACS("GetCrosshair", 9) )
		TNT1 A 0 A_GunFlash
		TNT1 A 1
		TNT1 A 0 A_JumpIfInventory("IsRunning", 1, "StartSprint")
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1, 3)
		TNT1 A 0 A_JumpIfInventory("NoAutoReload", 1, 5)
		TNT1 A 0 A_JumpIfInventory("GLAmmo", 1, 4)
		TNT1 A 0 A_JumpIfInventory("GLLoaded", 1, 3)
		TNT1 AA 0 A_WeaponReady(WRF_NOFIRE)
		Goto RaiseReloading
		TNT1 AA 0 
	ResumeNormalRaise:
		TNT1 A 0 A_JumpIfInventory("NeverSelectedGL",1,"FirstSelectAnim")
		TNT1 A 0 A_JumpIfInventory("AmmoRocket",1,1)
		Goto RaiseNoAmmo
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" Z 1 Offset(30, 50)
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" J 1 Offset (1,33) A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_PlaySound("GrenadeLauncher/Close", 5)
		"####" I 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		2RL2 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRL2 A 0
		"####" HIJKL 1 A_WEAPONREADY(WRF_NOBOB|WRF_ALLOWZOOM)
		Goto Ready

	RaiseNoAmmo:
		GRLN A 0 A_PlaySound("Rocket/Draw", 5)
		GRLN A 0 A_GunFlash
		2RL2 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRL2 A 0
		"####" AA 0
		"####" MNOP 1 A_WeaponReady(WRF_NOFIRE)
		"####" A 0 A_TakeInventory("RELOADING",1)
		Goto Ready

	FirstSelectAnim:
		TNT1 A 0 A_TakeInventory("NeverSelectedGL",1)
		GRLN A 0 A_JumpIfInventory("IsRunning", 1, "StartSprint")
		GRL2 VU 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRLN LLMNOOP 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRLN QRTTUU 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("GrenadeLauncher/Load", 5)
		GRLN UVWXY 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 AAAA 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRLN Z 1 A_WeaponReady(WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("GrenadeLauncher/Close", 5)
		GRLN "[" 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRLN "\]" 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRL2 ABCC 1 A_WeaponReady(WRF_ALLOWZOOM)
		GRL2 DDEFGHIJKL 1 A_WeaponReady(WRF_ALLOWZOOM)
		Goto Ready

	SelectAnimation:
	Ready:
	Ready3:
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		"####" A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		"####" A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		"####" A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" A 0 A_JumpIfInventory("IsRunning", 1, "StartSprint")
		"####" A 1 A_WeaponReady//(WRF_ALLOWRELOAD)
		"####" A 0 A_JumpIfInventory("Unloading", 1, "Unload")
		"####" A 0 A_JumpIfInventory("Reloading", 1, "Reload")
		Goto Ready

	Deselect:
		GRLN A 0 A_Lower
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("HoldTheGL",1)
		2RL2 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRL2 A 0
		"####" QRST 1
		TNT1 AAAAAAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait

	Fire:
		TNT1 A 0
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" A 0 
		"####" A 0 A_TakeInventory("Reloading",1)
		"####" A 0 A_TakeInventory("HoldTheGL",1)
		"####" A 0 A_JumpIfInventory("GLAmmo", 1, 1)
		Goto NoAmmo
		"####" A 0 A_TakeInventory("GLAmmo", 1)
		"####" A 0 A_JumpIfInventory("NoReloading",1,"NoReload")
		Goto ResumeFireFrame

	NoReload:
		"####" A 0 A_JumpIfInventory("AmmoRocket", 1, 1)
		Goto NoAmmo
		"####" A 0 A_TakeInventory("AmmoRocket", 1,TIF_NOTAKEINFINITE)
	ResumeFireFrame:
		"####" A 0
		"####" A 0 A_AlertMonsters
		"####" A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
		"####" AAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-16,16), 0, -1, random(-9,9))
		"####" A 0 A_PlaySound("GrenadeLauncher/Fire", 1)
		"####" A 0 A_FireCustomMissile("GrenadeMissileBreaksGlass", 0, 0, 0, 0)
		"####" A 0 A_ZoomFactor(0.95)
		"####" A 0 A_Recoil(1)
		"####" A 0 A_TakeInventory("GLLoaded",1)
		"####" A 0 A_FireCustomMissile("ShortGrenade2", 0, 1, 0, 0)
		"####" B 1 A_SetPitch(pitch - 1)
		"####" A 0 A_ZoomFactor(1.0)
		"####" C 1 A_SetPitch(pitch - 2)
		"####" DE 1 A_SetPitch(pitch + 1)
		"####" EAAA 1 A_SetPitch(pitch + 0.5)
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1, 2)
		"####" A 0 A_JumpIfInventory("NoAutoReload", 1, "Ready")
		"####" AA 0
		"####" A 0 A_JumpIfInventory("AmmoRocket",1,"Reload")
		Goto NoAmmo2

	AltFire:
		TNT1 A 0
		TNT1 A 0 A_TakeInventory("HoldTheGL",1)
		GRLN A 0 A_JumpIfInventory("GLAmmo", 1, 1)
		Goto NoAmmo
		"####" A 0 A_TakeInventory("GLAmmo", 1)
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"AltNoReload")
		GRLN A 0 A_JumpIfInventory("NoReloading",1,"AltNoReload")
		Goto ResumeAltFireFrame

	AltNoReload:
		"####" A 0 A_JumpIfInventory("AmmoRocket", 1, 1)
		Goto NoAmmo
		"####" A 0 A_TakeInventory("AmmoRocket", 1,TIF_NOTAKEINFINITE)
	ResumeAltFireFrame:
		"####" A 0
		"####" A 0 A_AlertMonsters
		"####" A 0 A_FireCustomMissile("RedFlareSpawn",-5,0,0,0)
		"####" AAAAAA 0 BRIGHT A_FireCustomMissile("ShotgunParticles", random(-16,16), 0, -1, random(-9,9))
		"####" A 0 A_PlaySound("GrenadeLauncher/Fire", 1)
		"####" A 0 A_FireCustomMissile("GrenadeMissileBreaksGlass", 0, 0, 0, 0)
		"####" A 0 A_ZoomFactor(0.95)
		"####" A 0 A_Recoil(1)
		"####" A 0 A_TakeInventory("GLLoaded",1)
		"####" A 0 A_FireCustomMissile("GrenadeMissile", 0, 1, -1, 0)
		"####" B 1 A_SetPitch(pitch - 1)
		"####" A 0 A_ZoomFactor(1.0)
		"####" C 1 A_SetPitch(pitch - 2)
		"####" DE 1 A_SetPitch(pitch + 1)
		"####" EAAA 1 A_SetPitch(pitch + 0.5)
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1, 2)
		"####" A 0 A_JumpIfInventory("NoAutoReload", 1, "NoAmmo2")
		"####" AA 0
		"####" A 0 A_JumpIfInventory("AmmoRocket",1,"Reload")
		Goto NoAmmo2

	Reload:
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" A 0
		"####" A 0 A_TakeInventory("Reloading",1)
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,3)
		"####" A 0 A_JumpIfInventory("HoldTheGL",1,"Inspect2")
		"####" A 0 A_JumpIfInventory("GLAmmo", 1, "Inspect")
		"####" A 0 A_JumpIfInventory("GLLoaded", 1, "Ready")
		"####" A 0 A_JumpIfInventory("AmmoRocket", 1, 1)//Check if has any ammo left to reload
		Goto Ready
		"####" FGHII 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
	RaiseReloading:
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" A 0 A_JumpIfInventory("AmmoRocket", 1, 1)//Check if has any ammo left to reload
		Goto ResumeNormalRaise
		"####" J 1 A_PlaySound("GrenadeLauncher/Open", 5)
		"####" K 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_JumpIfInventory("GLReloadToken",1,3)
		"####" A 0  A_FireCustomMissile("GrenadeCasingSpawner",0,0,8, -14)
		"####" A 0 A_FireCustomMissile("SmokeSpawner",0,0,0,5)
		"####" AA 0 
		"####" A 0 A_GiveInventory("GLReloadToken", 1)
		"####" LLMNOOP 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" QRTTUU 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_GiveInventory("GLAmmo", 1)
		"####" A 0 A_GiveInventory("GLLoaded",1)
		//"####" A 0 A_JumpIfInventory("IsPlayingAsPurist",1,3)
		"####" A 0 A_JumpIfInventory("NoReloading",1,2)
		"####" A 0 A_TakeInventory("AmmoRocket", 1,TIF_NOTAKEINFINITE)
		"####" A 0 A_TakeInventory("GLReloadToken", 1)
		"####" A 0 A_PlaySound("GrenadeLauncher/Load", 5)
		"####" UVWXY 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 4 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" Z 1 Offset (1,33) A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" a 0 A_PlaySound("GrenadeLauncher/Close", 5)
		"####" "[" 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" "\]" 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		2RL2 A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRL2 A 0
		"####" ABCC 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" DDEFGHIJKL 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		"####" A 0 A_TakeInventory("Reloading", 1)
		Goto Ready

	Unload:
		GRLN A 1
		TNT1 A 0 A_TakeInventory("Unloading", 1)
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"PuristWarning")
		TNT1 A 0 A_JumpIfInventory("NoReloading",1,"Ready3")
		TNT1 A 0 A_JumpIfInventory("GLAmmo", 1, 2)
		Goto Ready
		TNT1 AA 0
		GRLN FGHII 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		GRLN J 1 A_PlaySound("GrenadeLauncher/Open", 5)
		GRLN K 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_GiveInventory("AmmoRocket", 1)
		TNT1 A 0 A_TakeInventory("GLAmmo", 1)
		TNT1 A 12 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		GRLN Z 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_PlaySound("GrenadeLauncher/Close", 5)
		GRLN "[" 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		GRLN "\]" 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		GRL2 ABCC 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		GRL2 DDEFGHIJKL 1 A_WEAPONREADY(WRF_NOBOB|WRF_NOFIRE|WRF_ALLOWZOOM)
		TNT1 A 0 A_TakeInventory("Unloading", 1)
		TNT1 A 0 A_TakeInventory("Reloading", 1)
		Goto Ready

	NoAmmo:
		2RLN A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,2)
		GRLN A 0
		"####" A 0
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_Takeinventory("Zoomed",1)
		"####" A 0 A_Takeinventory("ADSmode",1)
		"####" A 0 A_PlaySound("Weapon/Empty", 5)
	NoAmmo2:
		"####" A 0 A_JumpIfInventory("TossGrenade",1,"TossGrenade")
		"####" A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		"####" A 0 A_JumpIfInventory("Salute1", 1, "Salute")
		"####" A 0 A_JumpIfInventory("Salute2", 1, "Salute")
		"####" A 0 A_JumpIfInventory("IsRunning",1,"StartSprint")
		"####" A 0 A_JumpIfInventory("ExecuteDownedEnemy", 1, "DoExecution")
		"####" A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		"####" A 5 A_WeaponReady(WRF_ALLOWZOOM | WRF_NOFIRE)
		"####" A 0 A_JumpIfInventory("Reloading",1,"Reload")
		"####" A 0 A_JumpIfInventory("GLAmmo", 1, "Ready3")
		"####" A 0 A_JumpIfInventory("IsPlayingAsPurist", 1,"Reload")
		"####" A 0 A_JumpIfInventory("NoAutoReload", 1, "NoAmmo2")
		"####" A 0 A_JumpIfInventory("AmmoRocket",1,"Reload")
		"####" A 0 A_JumpIfInventory("FiredPrimary",1,"NoAmmo2")
		Goto Ready3

	StartSprint:
		TNT1 A 0 A_TakeInventory("NeverSelectedGL",1)
		GRL2 XYZ 1 A_WeaponReady(WRF_NOFIRE)
		GRL2 "[" 0 A_JumpIfInventory("UsedStamina", 40, "StopSprintTired")
	Sprinting:
		GRL2 "[" 1
		GRL2 "[" 0 offset(-9,32)
		//GRL2 "[" 0 offset(-9,32) A_JumpIfInventory("IsStandingStill", 1, "Ready3")
		GRL2 "[" 0 offset(-9,32) A_JumpIfInventory("UsedStamina", 100, "StopSprintTired")
		PLAY A 0 offset(-9,32) ACS_ExecuteAlways(852, 0, 0, 0, 0)//Makes player faster.
		GRL2 "[" 0 offset(-9,32) A_JumpIfInventory("PowerStrength", 1, 2)
		GRL2 "[" 0 offset(-9,32) A_GiveInventory("UsedStamina", 5)
		GRL2 "[" 0 offset(-9,32)
		GRL2 "[" 1 offset(-9,34) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(-6,36) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(-3,38) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(0,38) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(3,36) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(6,34) A_SetPitch(pitch -0.5)
		GRL2 "[" 1 offset(9,32) A_SetPitch(pitch -0.5)
		GRL2 "[" 0 offset(9,32) A_WeaponReady(WRF_NOBOB)
		GRLN A 0 A_JumpIfInventory("Kicking",1,"DoKick")
		GRLN A 0 A_JumpIfInventory("Taunting",1,"Taunt")
		GRLN A 0 A_JumpIfInventory("Reloading",1,"Reload")
		GRL2 "[" 0 offset(-9,32) A_GiveInventory("UsedStamina", 5)
		GRL2 "[" 0 offset(9,33) A_SpawnItemEx("FootStep", 0, 0, 2, 0, 0, -4)
		GRL2 "[" 1 offset(9,34) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(6,36) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(3,38) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(0,38) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(-3,36) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(-6,34) A_SetPitch(pitch +0.5)
		GRL2 "[" 1 offset(-9,32) A_SetPitch(pitch +0.5)
		GRL2 "[" 0 offset(-9,32) A_WeaponReady(WRF_NOBOB)
		GRL2 "[" 0 offset(-9,32) A_JumpIfInventory("IsRunning", 1, "Sprinting")
		Goto StopSprint

	StopSprintTired:
		GRL2 ZYX 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		GRLN A 0 A_PlaySound("Tired", 2)
		GRLN A 0 A_TakeInventory("UsedStamina", 2)
		GRLN A 5 A_WeaponReady
		GRLN A 0 A_TakeInventory("UsedStamina", 2)
		GRLN A 5 A_WeaponReady
		GRLN A 0 A_TakeInventory("UsedStamina", 2)
		GRLN A 5 A_WeaponReady
		GRLN A 0 A_TakeInventory("UsedStamina", 2)
		GRLN A 5 A_WeaponReady
		GRLN A 0 A_TakeInventory("UsedStamina", 2)
		GRLN A 5 A_WeaponReady
		Goto Ready

	StopSprint:
		GRLN A 0 A_JumpIfInventory("UsedStamina", 60, "StopSprintTired")
		GRL2 ZYX 1
		PLAY A 0 ACS_ExecuteAlways(853, 0, 0, 0, 0)//Makes player slower.
		Goto Ready

	Spawn:
		GLAP A -1
		Stop
	}
}

Actor GrenadeCasingSpawner : ShotcaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 Thing_ChangeTID(0,390)
		TNT1 A 0 A_CustomMissile("GrenadeCasing",0,0,random(80,100),2,random(50,70))
		Stop
	}
}
ACTOR GrenadeCasing
{
	Height 2
	Radius 2
	Speed 8
	Scale 0.7
	+DOOMBOUNCE
	-CANBOUNCEWATER
	- NOGRAVITY
	+WINDTHRUST
	+CLIENTSIDEONLY
	+MOVEWITHSECTOR
	+MISSILE
	+NOBLOCKMAP
	-DROPOFF
	+NOTELEPORT
	+FORCEXYBILLBOARD
	+NOTDMATCH
	+GHOST
	//+CANBOUNCEWATER
	//+DONTSPLASH
	Mass 2
	BounceFactor 0.4
	DeathSound "GrenadeLauncher/Casing"
	BounceSound "GrenadeLauncher/Casing"
	SeeSound "GrenadeLauncher/Casing"
	States
	{
	Spawn:
		GBPJ NNNOOOPPPIIIJJJKKKLLLMMM 1 A_CustomMissile("CasingSmoke", 0, 0, random(0, 360), 2, random(85, 90))
		Loop

	Death:
		"####" "#" 0
		"####" "#" 0 ACS_NamedExecuteAlways("BDCheckJanitor")//Check Effects
		"####" "#" 0 ACS_NamedExecuteAlways("BDCheckJanitor2")//Check Effects
		"####" "#" 0 A_SpawnItem("MicroSmokeColumn")
	RestLoop:
		"####" "#" 200
		"####" "#" 0 A_JumpIfInventory("LowGraphicsMode", 1, "Stoping")
		Loop

	Stoping:
		TNT1 A 0
		Stop
	}
}